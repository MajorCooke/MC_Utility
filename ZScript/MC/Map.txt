Class MC_Map play
{
	static Vector2 RotateVector(Vector2 s, double angle, Vector2 rPoint)
	{
		s -= rPoint;
		return (s.x * cos(angle) - s.y * sin(angle),
				s.x * sin(angle) + s.y * cos(angle)) + rPoint;
	}
	
	static Vector2 RotateVectorOffset(Vector2 v, double angle)
	{
		double c = cos(angle),	s = sin(angle);
		
		return (v.x * c + v.y * s,
				v.x * s - v.y * c);
	}
	
	static Vector3 GetWarpPos(LevelLocals lvl, Vector3 pos, Vector3 off, double angle)
	{
		off.XY = MC_Map.RotateVectorOffset(off.XY, angle);
		return lvl.Vec3Offset(pos, off);
	}
	
	static double SnapDouble(double v, double gridsize)
	{
		v = round(v / gridsize) * gridsize;
		return v;
	}
	static Vector2 SnapVector2(Vector2 v, Vector2 gridsize)
	{
		v.x = round(v.x / gridsize.x) * gridsize.x;
		v.y = round(v.y / gridsize.y) * gridsize.y;
		return v;
	}
	
	static Vector2 SnapVector2Uniform(Vector2 v, double gridsize)
	{
		v.x = round(v.x / gridsize) * gridsize;
		v.y = round(v.y / gridsize) * gridsize;
		return v;
	}
	
	static Vector3 SnapVector3(Vector3 v, Vector3 gridsize)
	{
		v.x = round(v.x / gridsize.x) * gridsize.x;
		v.y = round(v.y / gridsize.y) * gridsize.y;
		v.z = round(v.z / gridsize.z) * gridsize.z;
		return v;
	}
	
	static Vector3 SnapVector3Uniform(Vector3 v, double gridsize)
	{
		v.x = round(v.x / gridsize) * gridsize;
		v.y = round(v.y / gridsize) * gridsize;
		v.z = round(v.z / gridsize) * gridsize;
		return v;
	}

	// Code by PhantomBeta.
	static Vector2, Vector2 SectorBounds (Sector sec) 
	{
		Vector2 posMin = ( double.Infinity,  double.Infinity);
		Vector2 posMax = (-double.Infinity, -double.Infinity);

		for (int i = 0; i < sec.lines.Size (); i++) 
		{
			Line l = sec.Lines [i];
			
			posMin = (
				min (posMin.X, l.v1.p.X, l.v2.p.X),
				min (posMin.Y, l.v1.p.Y, l.v2.p.Y));
			posMax = (
				max (posMax.X, l.v1.p.X, l.v2.p.X),
				max (posMax.Y, l.v1.p.Y, l.v2.p.Y));
		}
		return posMin, posMax;
	}
	
	//==========================================================================
	// A test function to count the number of lines.
	//==========================================================================
	static int, int CountIntersectingLines(Actor mo, double radius = -1.0)
	{
		if (!mo || (mo.radius <= 0 && radius <= 0.0))	return -1, -1;
		
		if (radius <= 0.0)
			radius = mo.radius;
	
		// Find the line that this actor's centerpoint is closest to
		double radius = mo.radius;
		Vector3 pos = mo.pos;
		
		double	xp = pos.x + radius,
				xn = pos.x - radius,
				yp = pos.y + radius,
				yn = pos.y - radius;
		
		
		int count[2]; count[0] = 0;	count[1] = 0;
		BlockLinesIterator it = BlockLinesIterator.Create(mo,radius);
		while (it.Next())
		{
			Line current = it.curline;
			
			count[0]++;
			
			// Discard lines that don't intersect the actor's radius AT ALL.
			if ((current.v1.p.x > xp && current.v2.p.x > xp) ||
				(current.v1.p.x < xn && current.v2.p.x < xn) ||
				(current.v1.p.y > yp && current.v2.p.y > yp) ||
				(current.v1.p.y < yn && current.v2.p.y < yn))
				continue; 
			
			count[1]++;
		}
		return count[0], count[1];
	}
	
	//==========================================================================
	//==========================================================================
	
	static bool LineIntersects(Vector3 pos, double radius, Vector2 v1, Vector2 v2)
	{
		Vector2 POff = level.Vec2Offset((radius, radius), (0,0));
		Vector2 NOff = level.Vec2Offset((-radius, -radius), (0,0));
		
		if ((v1.x > POff.X && v2.x > POff.X) ||
			(v1.x < NOff.X && v2.x < NOff.X) ||
			(v1.y > POff.Y && v2.y > POff.Y) ||
			(v1.y < NOff.Y && v2.y < NOff.Y))
			return false;
		return true;
	}
	
	static MC_LineContainer GetIntersectingLines(Actor mo, Vector2 RH = (-1, -1), 
		bool usepos = false, Vector3 pos = (0,0,0), int mask = 0, 
		MC_LineContainer Container = null)
	{
		if (!mo && !usepos)	return null;
		if (RH.X <= 0.0)	
		{
			if (!mo)		return null;
			RH.X = mo.radius;
			if (RH.X <= 0.0)	return null;
		}
		
		if (mo && RH.Y <= 0.0)
			RH.Y = mo.height;
	
		// Find the line that this actor's centerpoint is closest to.
		if (!usepos)
			pos = mo.pos;
			
		Vector2 POff = pos.XY + RH,
				NOff = pos.XY - RH;
		
		if (!Container)	Container = MC_LineContainer.Create(mo, RH, pos);
		else Container.Clear();
		
		BlockLinesIterator it;
		if (usepos)		it = BlockLinesIterator.CreateFromPos(pos, RH.Y, RH.X);
		else			it = BlockLinesIterator.Create(mo);
			
		while (it.Next())
		{
			Line current = it.curline;
			
			if (mask != 0 && !(current.flags & mask))
				continue;
				
			Vector2 p1 = current.v1.p,
					p2 = current.v2.p;
			
			// Discard lines that definitely don't intersect the actor's radius
			if ((p1.x > POff.X && p2.x > POff.X) ||
				(p1.x < NOff.X && p2.x < NOff.X) ||
				(p1.y > POff.Y && p2.y > POff.Y) ||
				(p1.y < NOff.Y && p2.y < NOff.Y))
				{
					continue; 
				}
			
			Container.AddLine(current);
		}
		
		return Container;
	/*
		// Find the line that is closest based on proximity to end vertices
		double curdist = (current.v1.p - pos.xy + current.v2.p - pos.xy).Length();
		if (linedef == null || curdist < dist)
		{
			linedef = current;
			dist = curdist;
		}
	*/
	}
	
	static MC_SectorContainer GetIntersectingSectors(Actor mo, Vector2 RH = (-1, -1), 
		MC_LineContainer LC = null, bool usepos = false, Vector3 pos = (0,0,0))
	{
		if (!LC)
		{
			LC = MC_Map.GetIntersectingLines(mo, RH, true, pos);
			if (!LC)	return null;
			
		}
		
		let SC = MC_SectorContainer.Create(mo, RH);
		if (SC && LC)
		{
			for (int i = 0; i < LC.Size(); i++)
			{
				Line l = LC.Lines[i].lin;
				if (!l)	continue;
				
				SC.AddSector(l.frontsector);
				SC.AddSector(l.backsector);
			}
		}
		
		if (SC.Size() < 1)	
			SC.AddSector(Level.PointInSector(pos.XY));
		
		return SC;
	}
}

Class MC_Line play
{
	Line lin;
	
	static MC_Line Create(Line l)
	{
		if (!l)	return null;
		let ls = new('MC_Line');
		ls.lin = l;
		return ls;
	}
}

Class MC_LineContainer play
{
	Array<MC_Line> Lines;
	Actor mo;
	Vector2 RH;
	Vector3 pos;
	static MC_LineContainer Create(Actor mo = null, Vector2 RH = (-1,-1), Vector3 pos = (0,0,0))
	{
		let ls = new('MC_LineContainer');
		ls.Init(mo, RH, pos);
		return ls;
	}
	
	void Init(Actor mo, Vector2 RH, Vector3 pos)
	{
		Lines.Clear();
		self.mo = mo;
		self.RH = RH;
		self.pos = pos;
	}
	
	int Size()	{	return Lines.Size();	}
	
	void AddLine(Line lin)
	{
		if (!lin)	return;
		
		let linepart = MC_Line.Create(lin);
		if (linepart)
			Lines.Push(linepart);
	}
	
	void Clear()
	{
		Lines.Clear();
	}
	
	int Find(Line ld)
	{
		if (!ld)	return Lines.Size();
		int size = Lines.Size();
		for (int i = 0; i < size; i++)
		{
			if (!Lines[i] || Lines[i].lin != ld)	continue;
			return i;
		}
		return Lines.Size();
	}
}

Class MC_Sector play
{
	Sector sec;
	
	static MC_Sector Create(Sector s)
	{
		if (!s)	return null;
		let ls = new('MC_Sector');
		ls.sec = s;
		return ls;
	}
}

Class MC_SectorContainer play
{
	Array<MC_Sector> Sectors;
	Actor mo;
	Vector2 RH;
	int index;
	static MC_SectorContainer Create(Actor mo = null, Vector2 RH = (-1,-1))
	{
		let ls = new('MC_SectorContainer');
		ls.Sectors.Clear();
		ls.mo = mo;
		ls.RH = RH;
		return ls;
	}
	
	int Size()	{	return Sectors.Size();	}
	
	void AddSector(Sector sect)
	{
		if (!sect)	return;
		
		for (int i = 0; i < Sectors.Size(); i++)
		{
			if (Sectors[i] && Sectors[i].sec == sect)
				return;
		}
		
		let secpart = MC_Sector.Create(sect);
		if (secpart)
			Sectors.Push(secpart);
	}
}

Class MC_SecPlane play
{
	SecPlane sec;
	
	static MC_SecPlane Create(SecPlane s)
	{
		if (!s)	return null;
	
		let ls = new('MC_SecPlane');
		ls.sec = s;
		return ls;
	}
}

Class MC_SecPlaneContainer play
{
	Array<MC_SecPlane> SecPlanes;
	
	static MC_SecPlaneContainer Create()
	{
		let ls = new('MC_SecPlaneContainer');
		ls.SecPlanes.Clear();
		return ls;
	}
	
	int Size()	{	return SecPlanes.Size();	}
	
	void AddSecPlane(SecPlane secp)
	{
		if (!secp)	return;
		
		for (int i = 0; i < Size(); i++)
		{
			if (SecPlanes[i] && SecPlanes[i].sec == secp)
				return;
		}
		
		let secpart = MC_SecPlane.Create(secp);
		if (secpart)
			SecPlanes.Push(secpart);
	}
}

Class CVector2
{
	Vector2 V;
	static CVector2 Create(Vector2 a)
	{
		let vec = new('CVector2');
		vec.V = a;
		return vec;
	}
}

Class CVector3
{
	Vector3 V;
	static CVector3 Create(Vector3 a)
	{
		let vec = new('CVector3');
		vec.V = a;
		return vec;
	}
}