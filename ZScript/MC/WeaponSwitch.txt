// [MC] Code based off of Marisa Kirisame's NotHudMessage classes. This is needed
// to display the weapon tag. Long, arduous and tiresome work-around is just that...
// UGH.
Class QueuedWeapon
{
	Weapon weap;
	int timestamp;
	static clearscope QueuedWeapon Create(Weapon next)
	{
		if (!next)	return null;
		let q = new('QueuedWeapon');
		q.weap = next;
		q.timestamp = gametic;
		return q;
	}
	
	ui void PrintTag()
	{
		if (weap != null)
			weap.DisplayNameTag();
	}
}

//==============================================================================
//==============================================================================
// MCWeaponList
// 
// This class is responsible for finding all weapons when the game loads up.
// Used for determining the next weapon slot when pressing a slot number.
//
// In order for this to work, a weapon must have "Weapon.SlotNumber" and
// "Weapon.SlotPriority" assigned.
//
// "Weapon.SlotNumber" must be between 0 and 9
//==============================================================================
//==============================================================================

Class MCWeaponList play
{
	Array<Class<Weapon> > Weapons;
	
	int GetSlotNumber(readonly<Weapon> defwep)
	{
		if(!defwep)
			return -1;
		if(defwep.SlotNumber>=0&&defwep.SlotNumber<=9)
			return defwep.SlotNumber;
		//If the weapon didn't define a slotnumber, check the playerclasses
		//to see if one of them has it as a weaponslot
		int playerend=PlayerClasses.Size();

		string thisslot;
			int theseweaponssize,found;
		Array<String> theseweapons;
			let wepname=defwep.GetClassName();

		for(int i=0;i<playerend;i++)
		{
			//Check this playerclass to see if the weapon's in its slots
			let playerclass=(class<PlayerPawn>)(PlayerClasses[i].type);
			let playerdef=GetDefaultByType(playerclass);
			for(int j=0;j<=9;j++)
			{
				//Split this slot into a string array
				thisslot=playerdef.slot[j];
				thisslot.Split(theseweapons," ");
				theseweaponssize=theseweapons.size();
				found=theseweapons.find(wepname);
				//If this weapon's name is in this array (is an item in this weaponslot)
				//Choose this to be its slot for all other purposes!
				if(found<theseweaponssize)
					return j;
				//If not, clear that array and try again
				theseweapons.clear();
			}
		}
		//Still didn't find a slot in all those players?
		//Weapon must not be slotted anywhere. Let's kill it...
		return -1;
	}
	void Init()
	{
		// First, just get all weapon valid classes.
		Array<Class<Weapon> > Unsorted;
		Weapons.Clear();
		Unsorted.Clear();
		for (int i = 0; i < AllActorClasses.Size(); ++i)
		{
			// Validate first.
			let wep = (Class<Weapon>)(AllActorClasses[i]);
			if (!wep)	continue;
			
			// Skip all bad assignments.
			//let def = GetDefaultByType(wep);
			//if (def.SlotNumber < 0 || def.SlotNumber > 9 || 
				//def.SlotPriority > 1.0 || def.SlotPriority < 0.0)
				//continue;
				
			Weapons.Push(wep);
		}
		
		// 5.998...
		// 5.999...
		// 4.000...
		// ...wait WHAT THE FUCK!?
		// Sadly that's the case here. I don't know why it was designed this way
		// internally, but it's crazy.
		// Sort through all weapons based on those schematics. Surprisingly,
		// this was a little easier than I thought it would be.
		
		int size = Weapons.Size();
		for (int i = 0; i < size - 1; i++)
		{
			for (int j = 0; j < size - i - 1; j++)
			{
				let UnDef = GetDefaultByType(Weapons[j]);
				let WeDef = GetDefaultByType(Weapons[j + 1]);
				int UnSlot = GetSlotNumber(UnDef);
				int WeSlot = GetSlotNumber(WeDef);
				
				// Put slot 0 weapons at the top by faking them to be 10.
				if (UnSlot == 0)	UnSlot = 10;
				if (WeSlot == 0)	WeSlot = 10;
				// First, we need to sort out based on slot. If it's behind,
				// switch it. This will group them all up together in DESCENDING
				// order.
				if (UnSlot > WeSlot)
				{
					Swap(j, j+1);
					continue;
				}
				
				// If they are the same slot, compare the priority and swap 
				// positions. as needed.
				else if (UnSlot == WeSlot)
				{
					if (UnDef.SlotPriority > WeDef.SlotPriority)
					{
						Swap(j, j+1);
					}
				}
			}
		}
		
		// Debug function for printing all VALID weapons thath ave been picked up
		// and sorted into the selection array. If a weapon doesn't display,
		// that means the slot number and/or priority is set wrong.
		
		// Remember, slots must be in range [1,10] and the priority must be [0.0, 1.0].
		// Weapons that have conflicting priorities are a non-issue, they're just
		// randomly sorted based on which is registered first.
		
		// PrintWeaponSlots();
	}
	
	private void PrintWeaponSlots()
	{
		for (int i = Weapons.Size() - 1; i > -1; i--)
		{
			let defs = GetDefaultByType(Weapons[i]);
			Console.Printf("%d %.3f - %s", defs.SlotNumber, defs.SlotPriority, Weapons[i].GetClassName());
		}
	}
	
	// Helper function for the Init() above. Simply switches the positions that
	// are passed in.
	private void Swap(uint a, uint b)
	{
		Class<Weapon> temp = Weapons[a];
		Weapons[a] = Weapons[b];
		Weapons[b] = temp;
	}
	
	//==========================================================================
	// SetSlotWeapon
	// 
	// Replicates default weapon selecting just like regular GZDoom does it.
	// This function is called when using a 'slot <X>' command.
	//==========================================================================
	Weapon SetSlotWeapon(PlayerInfo player, int slot)
	{
		if (!player || !player.mo)	return null;
		
		if (slot < 0 || slot > 9)	return null;
		
		Actor plr = player.mo;
		Array<Class<Weapon> > SlotWeaps; SlotWeaps.Clear();
		
		int size = Weapons.Size();
		let wep = player.ReadyWeapon;
		let pend = player.PendingWeapon;
		
		// Get all applicable slot weapons and store them in a temporary array.
		for (int i = 0; i < size; i++)
		{
			let def = GetDefaultByType(Weapons[i]);
			if (def.SlotNumber == slot)
				SlotWeaps.Push(Weapons[i]);
		}
		
		// No weapons to select in this slot.
		if (SlotWeaps.Size() < 1)
			return null;
		
		// Get the weapon at the far end of the array if the player doesn't have
		// any selected by assigning the index to initialize failsafe measures.
		// The weapon at the far end is the one with the highest priority while
		// the close end has the lowest selection priority.
		Weapon next = null;
		size = SlotWeaps.Size();
		int index = size;
		if (wep)	index = SlotWeaps.Find(wep.GetClass());
		
		if (index >= size)
		{
			index = size - 1;
			next = Weapon(plr.FindInventory(SlotWeaps[index]));
			if (CheckWeaponValid(next))
			{
				return next;
			}
		}	
		
		// The player has a weapon so grab the next most valid one.
		int i = index;
		do
		{
			--i;
			if (i < 0)	i = size - 1;
			
			next = Weapon(plr.FindInventory(SlotWeaps[i]));
			if (CheckWeaponValid(next))
			{
				return next;
			}
			
		}
		while (i != index);
		return null;
	}
	
	//==========================================================================
	// SetNextWeapon
	//
	// Replaces 'weapnext' and 'weapprev' functionality.
	// This effectively relies upon the initialized weapon array being in
	// perfect order.
	//==========================================================================
	Weapon SetNextWeapon(PlayerInfo player, bool prev = false)
	{
		if (!player || !player.mo)	return null;
		Actor plr = player.mo;
		int size = Weapons.Size();
		Weapon wep = player.ReadyWeapon;
		Weapon pend = player.PendingWeapon;
		// No weapon or not in the array. Try to find one.
		if (!wep || Weapons.Find(wep.GetClass()) >= size)
		{
			for (int i = 0; i < size; i++)
			{
				let next = Weapon(plr.FindInventory(Weapons[i]));
				if (CheckWeaponValid(next))
				{
					return next;
				}
			}
		}
		
		// The array is already sorted upon map load, so all we need to do is
		// cycle through to the next array position and check if the weapon
		// is valid for switching.
		int index = Weapons.Find(wep.GetClass());
		int i = index;
		do
		{
			if (prev)
			{
				if (--i < 0)
					i = size - 1;
			}
			else
			{
				if (++i >= size)
					i = 0;
			}
			
			let next = Weapon(plr.FindInventory(Weapons[i]));
			if (pend != WP_NOCHANGE && pend == next)	continue;
			
			if (CheckWeaponValid(next))
			{
				return next;
			}
		} 
		while (i != index);
		return null;
	}
	
	// Helper function for Set<Next/Slot>Weapon. Checks to see if the player
	// has the weapon and the ammo for it if the DMflag is not enabled.
	private bool CheckWeaponValid(Weapon check)
	{
		return (check && check.Amount > 0 && 
			(sv_dontcheckammo || check.CheckAmmo(Weapon.EitherFire, false)));
	}
}

//==============================================================================
//==============================================================================
// Now for the handler itself, the code is handled in here via extension.
//==============================================================================
//==============================================================================

extend class MCHandlerStatic
{
	// Most of the code here is private because we don't want anything calling
	// it when not in game.

	// PostUITick
	private ui void PrintWeaponTags()
	{
		// load 'em up
		for ( int i=0; i<queue.size(); i++ )
			if ( queue[i].timestamp >= gametic )
				queue[i].PrintTag();
	}
	
	// WorldTick
	private play void TickWeaponTags()
	{
		for (int i = 0; i < Queue.size(); i++)
		{
			if (Queue[i].timestamp >= gametic) continue;
			Queue.Delete(i);
			i--;
		}
	}
	
	// InputProcess
	// This overrides the 'slot <X>/weapnext/weapprev' commands and comes first.
	// This is where it all begins.
	private ui bool CheckInputWeaponChange(InputEvent ev)
	{
		if (ev.Type == InputEvent.Type_KeyDown)
		{
			int index = consoleplayer;
			PlayerInfo player = players[index];
			
			// Make sure they exist, and they desire this.
			if (player)
			{
				let cv = CVar.GetCVar('MC_KeyconfWeaponBuster', player);
				if (!cv || !cv.GetInt())
					return false;
			}
			else return false;
			
			// They exist, but don't operate on dead/morphed players.
			let plr = player.mo;
			if (!plr || plr.health < 1 || plr.FindInventory("PowerMorph",true))
				return false;
			
			static const String KeyBindsCCMDs [] = {
				"slot 1", "slot 2", "slot 3", "slot 4", "slot 5",
				"slot 6", "slot 7", "slot 8", "slot 9", "slot 0",
				"weapnext", "weapprev"	};
			static const String KeyBindsNetEvents [] = {
				"slot:1", "slot:2", "slot:3", "slot:4", "slot:5",
				"slot:6", "slot:7", "slot:8", "slot:9", "slot:0",
				"next", "prev"	};
			
			// Find the key and translate it from the raw command to an 
			// event-friendly string. Makes splitting it easier.
			int bind1, bind2;
			for (int i = 0; i < KeyBindsCCMDs.Size(); i++) 
			{
				// Bindings is a global struct. Definition in menu.txt inside GZDoom.pk3.
				// Get the keys that are bound to this action.
				[bind1, bind2] = Bindings.GetKeysForCommand (KeyBindsCCMDs [i]);
				
				if (ev.KeyScan == bind1 || ev.KeyScan == bind2) 
				{
					//Console.Printf("%s", KeyBindsNetEvents[i]);
					// Calls NetworkEvent in Base.txt and assembles the command
					// to call ChangeWeapons function below, and aborts the 
					// rest of the input.
					EventHandler.SendNetworkEvent(String.Format("MC_SelectWeapon:%s", KeyBindsNetEvents[i]));
					return true;
				}
			}
		}
		return false;
	}

	//==========================================================================
	// ChangeWeapons
	//
	// Sets the forthcoming weapon based on the command sent to it. Applicable
	// commands are:
	// 
	// slot # (0-9)
	// weapnext
	// weapprev
	//==========================================================================
	private Weapon ChangeWeapons(ConsoleEvent e)
	{
		int pnum = e.Player;
		if (!playeringame[pnum])
			return null;
			
		PlayerInfo player = players[pnum];

		if (!player) // Return if the player is null
			return null;

		Array<String> commandStrings;
		e.Name.Split (commandStrings, ":");
		if (commandStrings.Size() < 2) // Return if the array contains less than two strings
			return null;
			
		// At this point, we should have at minimum two strings in this form:
		// [0] = MC_SelectWeapon
		// [1] = <command>
		// See InputProcess for the translations (from first to second array)
		
		let ready = player.ReadyWeapon;
		let pending = player.PendingWeapon;
		
		if (!WeaponList)	
		{	
			WeaponList = new('MCWeaponList');
			WeaponList.Init();
		}
		
		Weapon next = null;
		if (commandStrings[1] == 'next')
		{
			next = WeaponList.SetNextWeapon(player);
		}
		else if (commandStrings[1] == 'prev')
		{
			next = WeaponList.SetNextWeapon(player, true);
		}
		else if (commandStrings[1] == 'slot')
		{
			// Switching to a weapon in slot.
			// MC_SelectWeapon:slot:#
			int len = (commandStrings.Size() > 2) ? commandStrings[2].Length() : 0;
			if (len < 1)	return null;
			
			String s = commandStrings[2];
			if (len > 1)	s = s.CharAt(len - 1);
			int num = s.ToInt();
			
			//Have a number.
			next = WeaponList.SetSlotWeapon(player, num);
		}
		if (!next)	next = ready;
		if (next && next != ready)	
			player.PendingWeapon = next;
		
		return next;
	}
}